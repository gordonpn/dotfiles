zsh_plugins=${ZDOTDIR:-~}/.zsh_plugins
[[ -f ${zsh_plugins}.txt ]] || touch ${zsh_plugins}.txt

fpath=(${ZDOTDIR:-~}/.antidote/functions $fpath)
autoload -Uz antidote

if [[ ! ${zsh_plugins}.zsh -nt ${zsh_plugins}.txt ]]; then
  antidote bundle <${zsh_plugins}.txt >|${zsh_plugins}.zsh
fi

HISTFILE=~/.zsh_history
setopt EXTENDED_GLOB
setopt GLOB_DOTS
HISTSIZE=10000000
SAVEHIST=$HISTSIZE
setopt appendhistory
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt AUTO_PUSHD
setopt PUSHD_IGNORE_DUPS
setopt PUSHD_SILENT

# FAST: Check typical locations without spawning the 'brew' process
if [[ -d "/opt/homebrew/share/zsh-completions" ]]; then
  FPATH="/opt/homebrew/share/zsh-completions:$FPATH"
elif [[ -d "/usr/local/share/zsh-completions" ]]; then
  FPATH="/usr/local/share/zsh-completions:$FPATH"
elif [[ -d "/home/linuxbrew/.linuxbrew/share/zsh-completions" ]]; then
  FPATH="/home/linuxbrew/.linuxbrew/share/zsh-completions:$FPATH"
fi

autoload -Uz compinit
# Optimization: Check cache once a day (speed up startup by ~100ms)
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

source ${zsh_plugins}.zsh

setopt MENU_COMPLETE
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
zstyle ':fzf-tab:complete:*:*' fzf-preview 'less ${(Q)realpath}'
zstyle ':fzf-tab:*' fzf-flags --height=35% --preview-window=right:60%
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/.zcompcache"

# 1) exact match first
# 2) then case-insensitive
# 3) then flex on separators like . _ -
# 4) then substring-ish matching
zstyle ':completion:*' matcher-list \
  '' \
  'm:{a-zA-Z}={A-Za-z}' \
  'r:|[._-]=* r:|=*' \
  'l:|=* r:|=*'

zshaddhistory() {
  setopt LOCAL_OPTIONS
  setopt EXTENDED_GLOB
  print -sr -- "${1%%$'\n'##}"
  fc -p "$HISTFILE"
  return 1
}

bracketed-paste() {
  zle .$WIDGET && LBUFFER=${LBUFFER%$'\n'}
}
zle -N bracketed-paste

if [[ "$(uname)" == "Darwin" ]]; then
  source /Users/gordonpn/workspace/dotfiles/dotfiles/.git-aliases
elif [[ "$(uname)" == "Linux" ]]; then
  source /home/gordonpn/dotfiles/dotfiles/.git-aliases
fi

alias ls='eza -lah --group-directories-first --icons'
alias cat='bat'
alias grep='rg'
alias find='fd'
alias du='dust'
alias df='duf'
alias ps='procs'
alias vi='nvim'
alias vim='nvim'
alias v='nvim'
alias repomix='pnpx repomix@latest'
alias ask="llm -m deepseek-chat"
alias reason="llm -m deepseek-reasoner"
alias qdrant="docker run --restart always -p 6333:6333 -v qdrant_storage:/qdrant/storage qdrant/qdrant"
alias treee='fd --type f --hidden --exclude .git | tree --fromfile'
alias tree='tree -a -I ".git|node_modules|.venv"'

pnpx() {
  if [ -f "node_modules/.bin/$1" ]; then
    pnpm exec "$@"
  else
    pnpm dlx "$@"
  fi
}

bindkey "$terminfo[kcuu1]" fzf-history-widget
bindkey "$terminfo[kcud1]" fzf-history-widget
bindkey '^[[A' fzf-history-widget
bindkey '^[[B' fzf-history-widget
bindkey '^[OA' fzf-history-widget
bindkey '^[OB' fzf-history-widget

bindkey '^R' fzf-history-widget
bindkey '^ ' autosuggest-accept

autoload -Uz edit-command-line
zle -N edit-command-line

# Insert Mode: Ctrl+v
bindkey -M viins '^v' edit-command-line

# Normal Mode: v (Standard Vi behavior)
bindkey -M vicmd 'v' edit-command-line

FNM_PATH="/opt/homebrew/opt/fnm/bin"
if [ -d "$FNM_PATH" ]; then
  eval "`fnm env`"
fi

[ -s "/Users/gordonpn/.bun/_bun" ] && source "/Users/gordonpn/.bun/_bun"

[[ "$TERM_PROGRAM" == "vscode" ]] && . "$(code --locate-shell-integration-path zsh)"

eval "$(zoxide init zsh)"
alias z='zi'
eval "$(starship init zsh)"
